# Run2 physics_params ----------------------------------------------------------
#     This rule runs the iterative procedure for FullRun2 years using RD.

rule toys_generate_data_run2:
  input:
    samples = lambda wcs: tuples(wcs,mode='Bs2JpsiPhi'),
    # Angular acceptance
    angacc_biased = 'output/params/angular_acceptance/{year}/Bs2JpsiPhi/{version}_{angacc}_{timeacc}_biased.json',
    angacc_unbiased = 'output/params/angular_acceptance/{year}/Bs2JpsiPhi/{version}_{angacc}_{timeacc}_unbiased.json',
    # Time acceptance coefficients
    timeacc_biased = 'output/params/time_acceptance/{year}/Bd2JpsiKstar/{version}_{timeacc}_biased.json',
    timeacc_unbiased = 'output/params/time_acceptance/{year}/Bd2JpsiKstar/{version}_{timeacc}_unbiased.json',
    # CSP
    csp_factors = lambda wcs: f'output/params/csp_factors/{wcs.year}/Bs2JpsiPhi/{wcs.version.split("@")[0]}.json',
    # Time resolution
    time_resolution = lambda wcs: f'output/params/time_resolution/{wcs.year}/Bs2JpsiPhi/{wcs.version.split("@")[0]}.json',
    # Flavor tagging
    flavor = lambda wcs: f'output/params/flavor_tagging/{wcs.year}/Bs2JpsiPhi/{wcs.version.split("@")[0]}.json',
    # Previously fitted parameters
    params = 'output/params/physics_params/run2/Bs2JpsiPhi/{version}_{fit}_{angacc}_{timeacc}.json',
  output:
    SAMPLES_PATH+'{year}/TOY_Bs2JpsiPhi/{version}w{fit}w{angacc}w{timeacc}gen{dateflag}.root',
  log:
    'output/log/toys_generate_data/{year}/TOY_Bs2JpsiPhi/{version}w{fit}w{angacc}w{timeacc}gen{dateflag}.log'
  run:
    shell(f"""(time\
    python analysis/toys/generator.py\
           --sample                  {input.samples}\
           --angacc-biased           {input.angacc_biased}\
           --angacc-unbiased         {input.angacc_unbiased}\
           --timeacc-biased          {input.timeacc_biased}\
           --timeacc-unbiased        {input.timeacc_unbiased}\
           --csp-factors             {input.csp_factors}\
           --time-resolution         {input.time_resolution}\
           --flavor-tagging          {input.flavor}\
           --fitted-params           {input.params}\
           --output                  {output}\
           --year                    {wildcards.year}\
           --version                 {wildcards.version}\
    ) &> {log}""")
    send_mail(f"Toy Generation RD :: {wildcards}", f"{log}")



rule toys_fit_run2a:
  wildcard_constraints:
    toyconf = ".*w.*w.*"
  input:
    samples = [SAMPLES_PATH+'2015/TOY_Bs2JpsiPhi/{version}w{toyconf}gen{dateflag}.root',
               SAMPLES_PATH+'2016/TOY_Bs2JpsiPhi/{version}w{toyconf}gen{dateflag}.root'],
    # Angular acceptance
    angacc_biased = [
      'output/params/angular_acceptance/2015/Bs2JpsiPhi/{version}_{angacc}_{timeacc}_biased.json',
      'output/params/angular_acceptance/2016/Bs2JpsiPhi/{version}_{angacc}_{timeacc}_biased.json'
    ],
    angacc_unbiased = [
      'output/params/angular_acceptance/2015/Bs2JpsiPhi/{version}_{angacc}_{timeacc}_unbiased.json',
      'output/params/angular_acceptance/2016/Bs2JpsiPhi/{version}_{angacc}_{timeacc}_unbiased.json'
    ],
    # Time acceptance coefficients
    timeacc_biased = [
      'output/params/time_acceptance/2015/Bd2JpsiKstar/{version}_{timeacc}_biased.json',
      'output/params/time_acceptance/2016/Bd2JpsiKstar/{version}_{timeacc}_biased.json'
    ],
    timeacc_unbiased = [
      'output/params/time_acceptance/2015/Bd2JpsiKstar/{version}_{timeacc}_unbiased.json',
      'output/params/time_acceptance/2016/Bd2JpsiKstar/{version}_{timeacc}_unbiased.json'
    ],
    # CSP
    csp_factors = lambda wcs: [
      f'output/params/csp_factors/2015/Bs2JpsiPhi/{wcs.version.split("@")[0]}.json',
      f'output/params/csp_factors/2016/Bs2JpsiPhi/{wcs.version.split("@")[0]}.json'
    ],
    # Time resolution
    time_resolution = lambda wcs: [
      f'output/params/time_resolution/2015/Bs2JpsiPhi/{wcs.version.split("@")[0]}.json',
      f'output/params/time_resolution/2016/Bs2JpsiPhi/{wcs.version.split("@")[0]}.json'
    ],
    # Flavor tagging
    flavor = lambda wcs: [
      f'output/params/flavor_tagging/2015/Bs2JpsiPhi/{wcs.version.split("@")[0]}.json',
      f'output/params/flavor_tagging/2016/Bs2JpsiPhi/{wcs.version.split("@")[0]}.json'
    ],
  output:
    params = 'output/params/physics_params/run2a/TOY_Bs2JpsiPhi/{version}w{toyconf}gen{dateflag}_run2a_{angacc}_{timeacc}.json',
    tables = 'output/tables/physics_params/run2a/TOY_Bs2JpsiPhi/{version}w{toyconf}gen{dateflag}_run2a_{angacc}_{timeacc}.tex'
  log:
    'output/log/physics_params_data/run2a/Bs2JpsiPhi/{version}_{toyconf}gen{dateflag}_run2a_{angacc}_{timeacc}.log'
  run:
    shell(f"""(time\
    python analysis/angular_fit/fit_data.py\
           --samples                 {",".join(input.samples)}\
           --angacc-biased           {",".join(input.angacc_biased)}\
           --angacc-unbiased         {",".join(input.angacc_unbiased)}\
           --timeacc-biased          {",".join(input.timeacc_biased)}\
           --timeacc-unbiased        {",".join(input.timeacc_unbiased)}\
           --csp                     {",".join(input.csp_factors)}\
           --time-resolution         {",".join(input.time_resolution)}\
           --flavor-tagging          {",".join(input.flavor)}\
           --params                  {output.params}\
           --tables                  {output.tables}\
           --year                    {",".join(YEARS['Run2a'])}\
           --flag                    run2a_{wildcards.angacc}_{wildcards.timeacc}\
           --version                 {wildcards.version}\
    ) &> {log}""")
    send_mail(f"Physics Parameters TOY Run2a", f"{log}")



rule toys_compute_pulls:
  wildcard_constraints:
    dateflag = '\d{6}(a|b|c|d|e|f|g)(.*)?',
    genconf = ".*w.*w.*w.*",
    fitconf = ".*_.*_.*"
  input:
    params = lambda wcs: [
      f'output/params/physics_params/run2a/TOY_Bs2JpsiPhi/{wcs.genconf}gen{wcs.dateflag}{i}_{wcs.fitconf}.json'
      for i in range(1,CONFIG['ntoys']+1)
    ]
  output:
    #params = 'output/params/systematics/run2a/TOY_Bs2JpsiPhi/{version}w{toyconf}gen{dateflag}_run2a_{angacc}_{timeacc}.json',
    tables = 'output/tables/systematics/fit_bias/{genconf}gen{dateflag}_{fitconf}.tex'
  log:
    'output/log/systematics/fit_bias/{genconf}gen{dateflag}_{fitconf}.log'
  run:
    shell(f"""(time\
    python analysis/toys/mans.py\
           --parameters              {",".join(input.params)}\
    ) #&> {log}""")
    #send_mail(f"Physics Parameters TOY Run2a", f"{log}")
