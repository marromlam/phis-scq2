# Rules for PID correction
# These sets of rules are very slow to run, so we should try to avoid
# reruning them
#
# TODO: create a set of rules to uplotad PID
# TODO: create some if-else structure to swicht runin/donwloading the PID
#       tuples -- should be quite easy


# correct the PID {{{

def input_for_correct_pid(wildcards):
    if "MC_Bs2DsPi" == f"{wildcards.mode}":
        # return SAMPLES + '/{year}/{mode}/{version}_{strip_sim}_pidgen{polarity}.root',
        return SAMPLES + '/{year}/{mode}/{version}_{strip_sim}_prepared{polarity}.root',
    else:
        return SAMPLES + '/{year}/{mode}/{version}_{strip_sim}_pidgen{polarity}.root',

rule scaling_tracks:
    "Corrects distributions of nTracks before going to PID correction"
    threads: 2
    resources:
        mem_mb = 12000
    wildcard_constraints:
        strip_sim = "str.*",
        polarity = "(Up|Down)",
        mode  = "MC_(Bs2JpsiPhi|Bd2JpsiKstar|Bs2JpsiPhi_dG0|Bu2JpsiKplus)"
    input:
        input_mc = expand(rules.preselection_add_genlvl_info.output.file,
                      mode='{mode}',
                      version='{version}',
                      strip_sim='{strip_sim}',
                      year='{year}',
                      polarity='{polarity}'),

        input_data = lambda wcs: expand(SAMPLES + '/{year}/{mode}/{version}_bkgsubtracted.root',
                                       mode=selconfig['gbw_input_data_mode'][wcs.mode],
                                       year = wcs.year,
                                       version = wcs.version
                                       ),
    params:
        target_weight = lambda wildcards: expand('\"sw\"' if selconfig['gbw_input_data_mode'][wildcards.mode] != 'Bs2JpsiPhi_Prompt'
                                                      else '\"\"'),
        var = '{"nTracks"}',
        treename = "DecayTree",
    output:
        plots = SAMPLES +'output/figures/plots/{mode}/{year}/nTracks_scaling_{version}_{strip_sim}_{polarity}',
        file = SAMPLES + '/{year}/{mode}/{version}_{strip_sim}_{polarity}_nTracks.root'
    shell:
        "root -l -q 'selection/gb_weights/TrackChi2Rescaling.C({params.var}, {params.target_weight}, \"{input.input_mc}\", \"{input.input_data}\", \"{output.file}\", \"{params.treename}\", \"{params.treename}\", \"{output.plots}\")'"



rule preselection_correct_pid:
    """
    Generic rule.
    Adds corrected PID variables to the given MC tuple
    """
    threads: 2
    resources:
        mem_mb = 12000
    wildcard_constraints:
        strip_sim = "str.*",
        polarity = "(Up|Down)",
        mode  = "(MC)((?!.*?(_fromLb)).*)"
    input:
        tracks_prob = 'selection/pid/pidcorr.yaml',
        tracks_pid = 'selection/pid/pidcorr_pid.yaml',
        file = input_for_correct_pid,
        # file = lambda wildcards: expand(rules.correct_muonpid.output.file if wildcards.mode != 'MC_Bs2DsPi' else rules.prepare_mc.output,
        #                                 version='{version}',
        #                                 mode='{mode}',
        #                                 strip_sim='{strip_sim}',
        #                                 year='{year}',
        #                                 polarity='{polarity}'),
    params:
        script = 'selection/tools/PIDCorr.py',
        var = config["pid_flag"]
    output:
        file = SAMPLES + '/{year}/{mode}/{version}_{strip_sim}_pidcorrected{polarity}.root',
        tmp1 = SAMPLES + '/{year}/{mode}/{version}_{strip_sim}_pidcorr{polarity}_tmp1.root',
        tmp2 = SAMPLES + '/{year}/{mode}/{version}_{strip_sim}_pidcorr{polarity}_tmp2.root',
    log:
        'output/log/selection_pid_correction/{year}/{mode}/{version}_{strip_sim}_pidcorrected{polarity}.log',
    run:
        if (config['stripping_mode']=='pidcuts') or ('Bu' in wildcards.mode):
          shell("(" + selconfig['runUrania'] +
                    " python {params.script} --input-file {input.file} \
                               --input-tree-name DecayTree \
                               --output-file {output.file} \
                               --data-set Mag{wildcards.polarity}_{wildcards.year} \
                               --mode {wildcards.mode} \
                               --tracks-file {input.tracks_prob} \
                               --var {params.var}\
                               --tmp1 {output.tmp1} \
                               --tmp2 {output.tmp2}) &> {log}")

        else:
          tmp3 = SAMPLES + f"/{wildcards.year}/{wildcards.mode}/{wildcards.version}_{wildcards.strip_sim}_pidgen{wildcards.polarity}_tmp3.root"
          tmp4 = SAMPLES + f"/{wildcards.year}/{wildcards.mode}/{wildcards.version}_{wildcards.strip_sim}_pidgen{wildcards.polarity}_tmp4.root"
          tmp5 = SAMPLES + f"/{wildcards.year}/{wildcards.mode}/{wildcards.version}_{wildcards.strip_sim}_pidgen{wildcards.polarity}_tmp5.root"

          shell("(" + selconfig['runUrania'] +
                    " python {params.script} --input-file {input.file} \
                               --input-tree-name DecayTree \
                               --output-file {tmp3} \
                               --data-set Mag{wildcards.polarity}_{wildcards.year} \
                               --mode {wildcards.mode} \
                               --tracks-file {input.tracks_prob} \
                               --var {params.var}\
                               --tmp1 {tmp4} \
                               --tmp2 {tmp5}) &> {log}")

          shell("(" + selconfig['runUrania'] +
                    " python selection/tools/PIDGen.py  --input-file {tmp3} \
                               --input-tree-name DecayTree \
                               --output-file {output.file} \
                               --data-set Mag{wildcards.polarity}_{wildcards.year} \
                               --mode {wildcards.mode} \
                               --tracks-file {input.tracks_pid} \
                               --var {params.var}\
                               --tmp1 {output.tmp1} \
                               --tmp2 {output.tmp2}) &>> {log}")

# }}}


# run gen PID {{{

rule preselection_correct_muonpid:
    """
    Generic rule.
    Adds corrected muon PID variables to the given MC tuple
    """
    threads: 2
    resources:
        mem_mb = 12000
    wildcard_constraints:
        strip_sim = "str.*",
        polarity = "(Up|Down)",
        mode  = "(MC)((?!.*?(_fromLb)).*)"
    input:
        tracks_prob = 'selection/pid/pidgen.yaml',
        tracks_pid = 'selection/pid/pidgen_pid.yaml',
        file = expand(rules.preselection_add_genlvl_info.output.file,
                      mode='{mode}',
                      version='{version}',
                      strip_sim='{strip_sim}',
                      year='{year}',
                      polarity='{polarity}'),
    params:
        script = 'selection/tools/PIDGen.py',
        var = config['pid_flag'],
    output:
        file = SAMPLES + '/{year}/{mode}/{version}_{strip_sim}_pidgen{polarity}.root',
        tmp1 = SAMPLES + '/{year}/{mode}/{version}_{strip_sim}_pidgen{polarity}_tmp1.root',
        tmp2 = SAMPLES + '/{year}/{mode}/{version}_{strip_sim}_pidgen{polarity}_tmp2.root',
    log:
        'output/log/selection_pid_correction/{year}/{mode}/{version}_{strip_sim}_pidgen{polarity}.log',
    run:
        if config['stripping_mode']=='pidcuts':
          shell("(" + selconfig['runUrania'] +
                    " python {params.script} --input-file {input.file} \
                               --input-tree-name DecayTree \
                               --output-file {output.file} \
                               --data-set Mag{wildcards.polarity}_{wildcards.year} \
                               --mode {wildcards.mode} \
                               --tracks-file {input.tracks_prob} \
                               --var {params.var}\
                               --tmp1 {output.tmp1} \
                               --tmp2 {output.tmp2}) &> {log}")
        else:
          tmp3 = SAMPLES + f"/{wildcards.year}/{wildcards.mode}/{wildcards.version}_{wildcards.strip_sim}_pidgen{wildcards.polarity}_tmp3.root"
          tmp4 = SAMPLES + f"/{wildcards.year}/{wildcards.mode}/{wildcards.version}_{wildcards.strip_sim}_pidgen{wildcards.polarity}_tmp4.root"
          tmp5 = SAMPLES + f"/{wildcards.year}/{wildcards.mode}/{wildcards.version}_{wildcards.strip_sim}_pidgen{wildcards.polarity}_tmp5.root"

          shell("(" + selconfig['runUrania'] +
                    " python {params.script} --input-file {input.file} \
                               --input-tree-name DecayTree \
                               --output-file {tmp3} \
                               --data-set Mag{wildcards.polarity}_{wildcards.year} \
                               --mode {wildcards.mode} \
                               --tracks-file {input.tracks_prob} \
                               --var {params.var}\
                               --tmp1 {tmp4} \
                               --tmp2 {tmp5}) &> {log}")

          shell("(" + selconfig['runUrania'] +
                " python {params.script} --input-file {tmp3} \
                               --input-tree-name DecayTree \
                               --output-file {output.file} \
                               --data-set Mag{wildcards.polarity}_{wildcards.year} \
                               --mode {wildcards.mode} \
                               --tracks-file {input.tracks_pid} \
                               --var {params.var}\
                               --tmp1 {output.tmp1} \
                               --tmp2 {output.tmp2}) &>> {log}")

# }}}


# vim: fdm=marker
